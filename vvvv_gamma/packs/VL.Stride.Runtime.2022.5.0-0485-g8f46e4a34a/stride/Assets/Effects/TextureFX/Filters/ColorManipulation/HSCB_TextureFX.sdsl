[Category("Filter")]
[Tags("hue, saturation, contrast, brightness")]
[Summary("Applies hue, saturation, contrast and brightness color correction")]
shader HSCB_TextureFX : FilterBase
{
    float Hue = .0f;
    float HueCycles = 1.0f;
    float Saturation = 1.0f;
    float SaturationBalance = .0f;
    float Contrast = .0f;
    float Brightness = .0f;

    float4 Filter(float4 tex0col)
    {
        float4 c = tex0col;
        float3 h = base.RGBtoHSL(c.rgb);
        h.y = pow(h.y, pow(2, SaturationBalance)) * Saturation;
        // todo debug: disable saturation below to see the glitches again
        // compare with hscb in beta 
        h.y = saturate(h.y);
        float3 k0 = base.HSLtoRGB(float3((frac(h.x + Hue) - 0) * HueCycles, h.y, h.z));
        float3 k1 = base.HSLtoRGB(float3((frac(h.x + Hue) - 1) * HueCycles, h.y, h.z));
        c.rgb = lerp(k0, k1, pow(smoothstep(0,1,h.x), 2));
        // change ' = ' to ' *= ' in line below for instant coolness
        // could we trigger that with a bool?
        c.rgb = normalize(c.rgb) * sqrt(3) * pow(length(c.rgb) / sqrt(3), pow(2,Contrast)); // cool spell, i wish i understood it
        c.rgb *= pow(2, Brightness);
        return c;
    }
};


