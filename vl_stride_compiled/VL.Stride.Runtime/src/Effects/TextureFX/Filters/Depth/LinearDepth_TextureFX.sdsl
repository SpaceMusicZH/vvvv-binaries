// source:
// https://github.com/mrvux/dx11-vvvv-girlpower/blob/master/nodes/texture11/Filter/LinearDepth.tfx

[Category("Filter")]
[Summary("")]
[OutputFormat("R32_Float")]
shader LinearDepth_TextureFX : FilterBase
{
    #define IS_ORTHO(P) (round(P._34)==0&&round(P._44)==1)
    #define linstep(a,b,x) saturate((x-a)/(b-a))
    
    float4x4 Projection;
    
    float MinDepth = 0.0f;
    float MaxDepth = 10.0f;

    bool Mapped = true;

    float4 Filter(float4 tex0col)
    {
        float2 uv = streams.TexCoord;
        float2 R = ViewSize;

        float4x4 tP = Projection;

        float d = Texture0.SampleLevel(PointSampler, uv, 0).x;
        float ld = 0;

        if(IS_ORTHO(tP))
        {
            ld = (d-tP._43)/tP._33;
        }
        else
        {
            ld = tP._43/(d - tP._33);
        }
        
        float4 c =  Mapped ? linstep(MinDepth, MaxDepth, ld) : ld;
        
        return c;
    }
};